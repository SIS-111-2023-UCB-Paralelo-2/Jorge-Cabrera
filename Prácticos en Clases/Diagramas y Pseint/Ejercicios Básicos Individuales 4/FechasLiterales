Funcion diaNumero <- ReglaDeTodosLosDias ( mes,diaNumero,elAñoEs )
	Si mes="01" o mes="03" o mes="05" o mes="07" o mes="08" o mes="10" o mes="12" Entonces
		Si diaNumero>0 y diaNumero<32 Entonces
			diaNumero=diaNumero
		SiNo
			Escribir "El día introducido no existe."
		Fin Si
	SiNo
		Si mes="04" o mes="06" o mes="09" o mes="11" Entonces
			Si diaNumero>0 y diaNumero<31 Entonces
				diaNumero=diaNumero
			SiNo
				Escribir "El día introducido no existe."
			Fin Si
		SiNo
			Si mes="02" Entonces
				Si elAñoEs="Bisiesto"  Entonces
					Si diaNumero>0 y diaNumero<30 Entonces
						diaNumero=diaNumero
					SiNo
						Escribir "El dia no existe"
					Fin Si
				SiNo
					Si elAñoEs="No Bisiesto" Entonces
						Si diaNumero>0 y diaNumero<29 Entonces
							diaNumero=diaNumero
						SiNo
							Escribir "El dia no existe"
						Fin Si
					SiNo
						Escribir "El dia no existe"
					Fin Si
				Fin Si
			SiNo
				Escribir "El mes no existe."
			Fin Si
		Fin Si
	Fin Si
Fin Funcion


Funcion elAñoEs <- ReglasDeAñoBisiesto( añoNumero )
	Si (añoNumero mod 4=0) y (añoNumero mod 100<>0) Entonces
		elAñoEs="Bisiesto"
	SiNo
		Si (añoNumero mod 100=0) y (añoNumero mod 400=0) Entonces
			elAñoEs="Bisiesto"
		SiNo
			elAñoEs="No Bisiesto"
		Fin Si
	Fin Si
Fin Funcion

Funcion mesEscrito <- MesVersionEscrita ( mes )
	Segun mes Hacer
		"01":
			mesEscrito="Enero"
		"02":
			mesEscrito="Febrero"
		"03":
			mesEscrito="Marzo"
		"04":
			mesEscrito="Abril"
		"05":
			mesEscrito="Mayo"
		"06":
			mesEscrito="Junio"
		"07":
			mesEscrito="Julio"
		"08":
			mesEscrito="Agosto"
		"09":
			mesEscrito="Septiembre"
		"10":
			mesEscrito="Octubre"
		"11":
			mesEscrito="Noviembre"
		"12":
			mesEscrito="Diciembre"
		De Otro Modo:
			Escribir "El mes no existente"
	Fin Segun
Fin Funcion

Algoritmo FechasLiterales
	Definir fecha,dia,mes,año,mesEscrito Como Caracter
	definir diaNumero,añoNumero Como Entero
	Escribir "Escriba una fecha siguiendo el siguiente ejemplo: 02-03-2023"
	Leer fecha
	
	dia=Subcadena(fecha,1,2)
	mes=SubCadena(fecha,4,5)
	año=Subcadena(fecha,7,10)
	
	añoNumero=ConvertirANumero(año)
	diaNumero=ConvertirANumero(dia)
	
	mesEscrito=MesVersionEscrita( mes )
	elAñoEs=ReglasDeAñoBisiesto( añoNumero )
	diaNumero=ReglaDeTodosLosDias ( mes,diaNumero,elAñoEs )
	Escribir "La fecha ingresada es: ",diaNumero," de ",mesEscrito," del ",año
FinAlgoritmo
